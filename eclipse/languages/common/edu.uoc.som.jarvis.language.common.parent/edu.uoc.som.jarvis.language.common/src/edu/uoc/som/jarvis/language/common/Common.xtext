grammar edu.uoc.som.jarvis.language.common.Common with org.eclipse.xtext.common.Terminals

import "edu.uoc.som.jarvis.common"

Program:
	(instructions += Instruction)*
;

Instruction:
	VariableDeclaration | Expression | ImportDeclaration
;

ImportDeclaration returns ImportDeclaration:
	{PlatformImportDeclaration} 'import platform' path=STRING ('as' alias=ID)?
	| {LibraryImportDeclaration} 'import library' path=STRING ('as' alias=ID)?
;

VariableDeclaration:
	'def' name=ID ('=' value=Expression)?
;

Expression:
	Operation | OperationCall
;

Operation:
	(UnaryOperation)
;

UnaryOperation:
	'!' exp=Expression
;

OperationCall returns Expression:
	PropertyAccess ({OperationCall.source = current} '.' name=ID '(' (args+=Expression (',' args+=Expression)*)? ')')*
;

PropertyAccess returns Expression:
	(Access | Literal) ({PropertyAccess.source = current}  '.' feature=ID)*
;

Access returns Expression:
	VariableAccess | ContextAccess
;

// if this returns an Expression then the generated xcore doesn't contain VariableAccess
VariableAccess:
	referredVariable = [VariableDeclaration|ID]
;

ContextAccess:
	{ContextAccess} 'context' '(' contextName=ID ')'
;

// if this returns an Expression then the generated xcore doesn't contain Literal
Literal:
	StringLiteral | NumberLiteral
;

StringLiteral:
	value = STRING
;

NumberLiteral:
	value = INT
;