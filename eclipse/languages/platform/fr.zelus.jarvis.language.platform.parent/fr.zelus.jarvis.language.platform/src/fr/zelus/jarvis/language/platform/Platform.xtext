grammar fr.zelus.jarvis.language.platform.Platform with fr.zelus.jarvis.language.intent.Intent

import "fr.zelus.jarvis.platform" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "fr.zelus.jarvis.intent" as intent

Platform returns PlatformDefinition:
	{PlatformDefinition}
	'Platform' name=EString
	'path' runtimePath=EString
	('providers' '{'
		eventProviderDefinitions+=(InputProviderDefinition|EventProviderDefinition)
		(eventProviderDefinitions+=(InputProviderDefinition|EventProviderDefinition))*
	'}')?
	('actions' '{' 
		actions+=Action 
		(actions+=Action)* 
	'}')?
	;
	
EventProviderDefinition returns EventProviderDefinition:
	{EventProviderDefinition}
	'event' name=EString
	'{'
		(eventDefinitions+=EventDefinition)*
	'}'
;

InputProviderDefinition returns InputProviderDefinition:
	{InputProviderDefinition}
	'input' name=EString 
;

Action returns ActionDefinition:
	{ActionDefinition}
	name=EString ('('parameters+=Parameter (',' parameters+=Parameter)*')')?;



Parameter returns Parameter:
	{Parameter}
	key=EString;

EString returns ecore::EString:
	STRING | ID('->'ID)*;
