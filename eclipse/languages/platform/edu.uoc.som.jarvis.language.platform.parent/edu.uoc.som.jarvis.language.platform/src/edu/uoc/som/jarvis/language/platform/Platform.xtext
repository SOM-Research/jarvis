grammar edu.uoc.som.jarvis.language.platform.Platform with edu.uoc.som.jarvis.language.intent.Intent

import "edu.uoc.som.jarvis.platform" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "edu.uoc.som.jarvis.intent" as intent

Platform returns PlatformDefinition:
	{PlatformDefinition}
	(imports+=PlatformImportDeclaration)*
	(abstract?='Abstract')?
	'Platform' name=EString
	('extends' extends=[PlatformDefinition|ID])?
	('path' runtimePath=EString)?
	('providers' '{'
		eventProviderDefinitions+=(InputProviderDefinition|EventProviderDefinition)
		(eventProviderDefinitions+=(InputProviderDefinition|EventProviderDefinition))*
	'}')?
	('actions' '{' 
		actions+=Action 
		(actions+=Action)* 
	'}')?
	;
	
PlatformImportDeclaration returns intent::PlatformImportDeclaration:
	{intent::PlatformImportDeclaration} 'import platform' path=STRING ('as' alias=ID)?
;	

EventProviderDefinition returns EventProviderDefinition:
	{EventProviderDefinition}
	'event' name=EString
	'{'
		(eventDefinitions+=EventDefinition)*
		(outContexts+=OutContextDefinition)*
		
	'}'
;

InputProviderDefinition returns InputProviderDefinition:
	{InputProviderDefinition}
	'input' name=EString 
	(
	'{'
	  (outContexts+=OutContextDefinition)*
	'}' 	
	)?
;

Action returns ActionDefinition:
	{ActionDefinition}
	name=EString ('('parameters+=Parameter (',' parameters+=Parameter)*')')?;



Parameter returns Parameter:
	{Parameter}
	key=EString;

EString returns ecore::EString:
	STRING | ID('->'ID)*;
