@Ecore(nsURI="edu.uoc.som.jarvis.intent")
@GenModel(modelDirectory="/intent/src/main/java-gen", importerID="org.eclipse.emf.importer.ecore", complianceLevel="8.0")
package edu.uoc.som.jarvis.intent

/*
 * Imports are defined in the intent metamodel because it is imported by all the other ones. However, a proper solution
 * would be to define a dedicated metamodel for language-related constructs (see #21)
 */
abstract class ImportDeclaration {
    String path
    String alias
}

class PlatformImportDeclaration extends ImportDeclaration {

}

class LibraryImportDeclaration extends ImportDeclaration {

}


class Library {
	String name
	contains EventDefinition[] eventDefinitions
}

class EventDefinition {
    String name
    contains Context[] outContexts
    op Context getOutContext(String contextName) {
        for(Context context : getOutContexts()) {
            // not case-sensitive comparison should be put in the documentation
            if(context.getName().toLowerCase().equals(contextName.toLowerCase())) {
                return context;
            }
        }
        return null;
    }
}

class IntentDefinition extends EventDefinition {
    refers IntentDefinition follows opposite followedBy
    refers IntentDefinition[] followedBy opposite follows
    contains Context[] inContexts
    String[] trainingSentences
}

class Context {
    String name
    int lifeSpan = "5"
    contains ContextParameter[] parameters
    op ContextParameter getContextParameter(String parameterName) {
        for(ContextParameter contextParameter : getParameters()) {
            if(contextParameter.getName().equals(parameterName)) {
                return contextParameter;
            }
        }
        return null;
    }
}

class ContextParameter {
    String name
    String textFragment
    contains EntityDefinition entity
}

class EventInstance {
    refers EventDefinition definition
    contains ContextInstance[] outContextInstances
    op ContextInstance getOutContextInstance(String contextInstanceName) {
        for(ContextInstance contextInstance : getOutContextInstances()) {
            if(contextInstance.getDefinition().getName().equals(contextInstanceName)) {
                return contextInstance;
            }
        }
        return null;
    }
}

class RecognizedIntent extends EventInstance {

}

class ContextInstance {
    refers Context definition
    int lifespanCount
    contains ContextParameterValue[] values opposite contextInstance
}

class ContextParameterValue {
    container ContextInstance contextInstance opposite values
    refers ContextParameter contextParameter
    String value
}

abstract class EntityDefinition {

}

class BaseEntityDefinition extends EntityDefinition {
    EntityType entityType
}

// class EntityDefinitionLink extends EntityDefinition {
//   refers EntityDefinition referredEntityDefinition -> for CompositeEntityDefinitions created in platforms
// }

// class CompositeEntityDefinition extends EntityDefinition

enum EntityType {
    ANY as "any" = 0
    DATE_TIME as "date-time" = 1
    DATE as "date" = 2
    DATE_PERIOD as "date-period" = 3
    TIME as "time" = 4
    TIME_PERIOD as "time-period" = 5
    NUMBER as "number" = 6
    CARDINAL as "cardinal" = 7
    ORDINAL as "ordinal" = 8
    INTEGER as "integer" = 9
    NUMBER_SEQUENCE as "number-sequence" = 10
    FLIGHT_NUMBER as "flight-number" = 11
    UNIT_AREA as "unit-area" = 12
    UNIT_CURRENCY as "unit-currency" = 13
    UNIT_LENGTH as "unit-length" = 14
    UNIT_SPEED as "unit-speed" = 15
    UNIT_VOLUME as "unit-volume" = 16
    UNIT_WEIGHT as "unit-weight" = 17
    UNIT_INFORMATION as "unit-information" = 18
    PERCENTAGE as "percentage" = 19
    TEMPERATURE as "temperature" = 20
    DURATION as "duration" = 21
    AGE as "age" = 22
    ADDRESS as "address" = 23
    STREET_ADDRESS as "street-address" = 24
    ZIP_CODE as "zip-code" = 25
    CAPITAL as "capital" = 26
    COUNTRY as "country" = 27
    COUNTRY_CODE as "country-code" = 28
    CITY as "city" = 29
    STATE as "state" = 30
    CITY_US as "city-us" = 31
    STATE_US as "state-us" = 32
    COUNTY_US as "county-us" = 33
    CITY_GB as "city-gb" = 34
    STATE_GB as "state-gb" = 35
    COUNTY_GB as "county-gb" = 36
    PLACE_ATTRACTION_US as "place-attraction-us" = 37
    PLACE_ATTRACTION_GB as "place-attraction-gb" = 38
    PLACE_ATTRACTION as "place-attraction" = 39
    AIRPORT as "airport" = 40
    LOCATION as "location" = 41
    EMAIL as "email" = 42
    PHONE_NUMBER as "phone-number" = 43
    GIVEN_NAME as "given-name" = 44
    LAST_NAME as "last-name" = 45
    MUSIC_ARTIST as "music-artist" = 46
    MUSIC_GENRE as "music-genre" = 47
    COLOR as "color" = 48
    LANGUAGE as "language" = 49
    URL as "url" = 50
}


